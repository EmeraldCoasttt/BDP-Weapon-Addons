class DeagleAmmo : Ammo {Default{Inventory.MaxAmount 8; Inventory.Icon "DEAGW0";}}

class DesertEagle : BrutalWeapon
{
	bool armBroke;
	
	Default
	{
		Weapon.AmmoUse1 0;
		Weapon.AmmoGive1 12;
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		Weapon.AmmoType1 "Clip1";
		Weapon.AmmoType2 "DeagleAmmo";
		Weapon.SlotNumber 2;
		Weapon.SlotPriority 4;
		Weapon.SelectionOrder 3800;
		Obituary "%o was shot down by %k's Desert Eagle.";
		Inventory.PickupSound "DEAGIN";
		Inventory.PickupMessage "You got the Desert Eagle! (Slot 2)";
		+WEAPON.NOAUTOAIM
		+WEAPON.NOAUTOFIRE
		+WEAPON.NOALERT
		Scale 0.9;
		Tag "Desert Eagle";
		Inventory.AltHUDIcon "DEAGW0";
		Inventory.MaxAmount 2;
		BrutalWeapon.DualWeapon "DualDeagle";
	}
	States
	{
	Ready:
		DEAG A 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		TNT1 A 0 A_JumpIf(invoker.aimMode == 0,3);
		DEAG Y 1 {A_ZoomFactor(1.0); invoker.aimMode = 0;}
		DEAG XMNOP 1;
		TNT1 A 0 A_Lower;
		Wait;
	Select:
		TNT1 A 0 {A_SetCrosshairDX("SMGRet",2000,0.2); invoker.aimMode = 0;}
		Goto SelectFirstPersonLegs;
	SelectContinue:
		TNT1 A 0 A_JumpIfInventory("SwitchingDualModes",1,"SelectFromDual");
		TNT1 A 0 A_JumpIf(invoker.armBroke,"SelectAfterFireOops");
		TNT1 A 1;
	ReturnFromNothing:
		TNT1 A 0 A_PlaySound("DEAGHOLD");
		DEAG PONM 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	SelectFromDual:
		TNT1 A 0 TakeInventory("SwitchingDualModes",1);
		DEA3 SRQPONM 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	 SelectAfterFireOops:
		TNT1 A 0 {invoker.armBroke = 0; invoker.initDual = 0; TakeInventory("DesertEagle",1);}
		DEA3 HIJKL 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	Fire:
		TNT1 A 0 A_CheckIfAmmo("DeagleAmmo");
		TNT1 A 0 A_SemiFlag;
		DEAG B 1 Bright {
			A_AlertMonstersDX;
			A_ZoomFactor(0.98);
			A_PlaySound("DISTDEAG",6);
			A_PlaySound("DEAGFIRE",1);
			A_TakeAmmo("DeagleAmmo",1);
			A_TakeAmmo("DualDeagleAmmo",1);
			A_FireBullets(0.35,0.25,-1,81,"ShotgunPuff",FBF_NORANDOM,0,"DecorativeTracer",-4);
		}
		DEAG C 1 Bright A_ZoomFactor(1.0);
		DEAG D 1 {
			A_Recoil(0.5);
			A_FireProjectile("DeagleCaseSpawn",5,0,0,-8);
			A_SetPitch(pitch-5,SPF_INTERPOLATE);
		}
		DEAG E 1 A_SetPitch(pitch-1,SPF_INTERPOLATE);
		DEAG FG 1;
		DEAG HIJK 1 A_SetPitch(pitch+1.5,SPF_INTERPOLATE);
		DEAG L 1;
		TNT1 A 0 A_SemiRefire("Fire","SemiAutoLul");
		Goto Ready;
	SemiAutoLul:
		DEAG A 1;
		TNT1 A 0 A_SemiRefire("Fire","SemiAutoLul");
		Goto Ready;
	AltFire:
		DEAG X 1 {A_PlaySound("ADSIN"); A_ZoomFactor(1.7);}
		DEAG Y 1;
		TNT1 A 0 A_StartAim;
		Goto Ready;
	Aim:
		DEA9 A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_CheckAim;
		Loop;
	AimEnd:
		DEAG Y 1 {A_PlaySound("ADSOUT"); A_ZoomFactor(1.0);}
		DEAG X 1;
		Goto Ready;
	FireAim:
		TNT1 A 0 A_CheckIfAmmo("DeagleAmmo",1,"NoAmmo3");
		TNT1 A 0 A_SemiFlag;
		DEA9 B 1 BRIGHT {
			A_AlertMonstersDX;
			A_ZoomFactor(1.78);
			A_PlaySound("DISTDEAG",6);
			A_PlaySound("DEAGFIRE",1);
			A_TakeAmmo("DeagleAmmo",1);
			A_TakeAmmo("DualDeagleAmmo",1);
			A_FireBullets(0.1,0.1,-1,81,"ShotgunPuff",FBF_NORANDOM,0,"DecorativeTracer",-4);
		}
		DEA9 C 1 BRIGHT A_ZoomFactor(1.7);
		DEA9 D 1 {
			A_Recoil(0.5);
			A_FireCustomMissile("DeagleCaseSpawn",5,0,0,-3);
			A_SetPitch(pitch-2,SPF_INTERPOLATE);
		}
		DEA9 E 1 A_SetPitch(pitch-1,SPF_INTERPOLATE);
		DEA9 FG 1;
		DEA9 HIJK 1 A_SetPitch(pitch+0.75,SPF_INTERPOLATE);
		DEA9 L 1;
		TNT1 A 0 A_SemiRefire("FireAim","SemiAutoLulAim");
		TNT1 A 0 A_CheckAim;
		Goto Aim;
	SemiAutoLulAim:
		DEA9 A 1;
		TNT1 A 0 A_SemiRefire("FireAim","SemiAutoLulAim");
		Goto Ready;
	NoAmmo:
		TNT1 A 0 A_StartSound("weapons/empty", 3);
		TNT1 A 0 A_JumpIfInventory("Clip1",3,"Reload");
	NoAmmo2:
		DEAG A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_JumpIf(player.cmd.buttons & BT_RELOAD || player.cmd.buttons & BT_ATTACK, "NoAmmo2");
		Goto Ready;
	NoAmmo3:
		TNT1 A 0 A_StartSound("weapons/empty", 3);
		TNT1 A 0 A_JumpIfInventory("Clip1",3,"Reload");
	NoAmmo4:
		DEA9 A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_JumpIf(player.cmd.buttons & BT_ATTACK, "NoAmmo4");
		Goto Aim;
	Reload:
		TNT1 A 0 A_JumpIf(invoker.aimMode == 0, 3);
		DEAG Y 1 {invoker.aimMode = 0; A_ZoomFactor(1.0);}
		DEAG X 1;
		TNT1 A 0 A_TakeInventory("Reloading",1);
		TNT1 A 0 A_JumpIfInventory("DeagleAmmo",7,"Ready");
		TNT1 A 0 A_CheckIfAmmo("Clip1",3);
		DEAR ABCD 1;
		DEAR E 1 {A_PlaySound("DEAGOUT"); A_FireCustomMissile("DeagleClipSpawn",-18,0,18,-15);}
		DEAR FGHIIIIJJKLMNOPPQQQRS 1;
		DEAR T 1 A_PlaySound("DEAGIN");
		DEAR UV 1;
		DEAR W 1 A_Reload("DeagleAmmo",7,"Clip1",1,3,"DualDeagleAmmo",16,0);
		DEAR X 1;
		DEAR Y 5;
		DEAR Z 1 A_PlaySound("DEAGSLID");
		DEA2 ABBBCDEFGH 1;
		Goto Ready;
	DualWield:
		TNT1 A 0 A_DualWield;
		Goto Ready;
	GrenadeThrowFlash:
		DEAG MNOP 1;
		TNT1 A 24;
		DEAG PONM 1; //32 in total
		Stop;
	KickingFlash:
		DEAG QRSTUUUUUUUUTSRQ 1; //16
		Goto Ready;
	AirKickingFlash:
		DEAG QRSTUUUUUUUUUUTSRQ 1; //18
		Goto Ready;
	SlideKickingStart:
		DEAG QRST 1;
	SlideKickingLoop:
		DEAG U 1 {
			if(CountInv("Kicking") == 0) {
				return resolvestate("SlideKickingEnd");
			}
			return resolvestate(null);
		}
		Loop;
	 SlideKickingEnd:
		DEAG UUUUUUTSRQ 1; //10
		Goto Ready;
	SprintOverlay:
		DEAG QRST 1;
	KeepSprinting:
		DEAG U 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE);
		"####" A 0 A_KeepSprinting;
	ReturnFromSprint:
		DEAG TSRQ 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE);
		Goto Ready;
	Spawn:
		DEAG W -1;
		Stop;
	}
	
	override void ondrop(Actor dropper)
	{
		giveinventory("AmmoTaken",1);
		if(dropper.findinventory("DualDeagle"))
		{
			giveinventory("DesertEagle",1);
			Inventory drop = FindInventory("DesertEagle");
			self.DropInventory(Drop,1);
		}
		dropper.TakeInventory("DualDeagle",1);
	}
}