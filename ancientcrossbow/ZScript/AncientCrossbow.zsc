class AncientCrossbow : BrutalWeapon
{
	int firemode;
	bool needsLoading;
	
	Default
	{
		Tag "Ancient Crossbow";
		Inventory.PickupMessage "You got the Crossbow! It looks ancient... (Slot 5)";
		Inventory.PickupSound "CrossSelect";
		Weapon.SlotNumber 5;
		Weapon.SlotPriority 4;
		Weapon.SelectionOrder 6100;
		Weapon.AmmoType "SoulAmmo";
		Weapon.AmmoGive 100;
		Weapon.AmmoUse 0;
		+WEAPON.NOALERT
		+WEAPON.NOAUTOAIM
		+WEAPON.NOAUTOFIRE
		Scale 0.8;
	}
	States
	{
	Ready:
		TNT1 A 0 A_JumpIf(invoker.needsLoading == 1,"ReadyEmpty");
		CRB2 A 0 A_JumpIf(invoker.firemode == 2,2);
		CRB1 A 0;
		"####" ABC 3 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		Loop;
	ReadyEmpty:
		CRSB A 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		TNT1 A 0 A_TakeInventory("IsPlayingDoxMod",1);
		CRSS A 0 A_JumpIf(invoker.needsLoading == 1,3);
		CR2S A 0 A_JumpIf(invoker.firemode == 2,2);
		CR1S A 0;
		"####" BCDE 1;
		TNT1 A 1;
		TNT1 A 0 A_Lower(128);
		Wait;
	Select:
		TNT1 A 0
		{
			A_SetCrosshairDX("UnmRet",800,1.0);
			if(invoker.firemode == 0)
			{
				invoker.firemode = 1;
			}
		}
		Goto SelectFirstPersonLegs;
	SelectContinue:
		TNT1 A 1 A_InstaRaise;
	ReturnFromNothing:
		CRSS A 0 A_JumpIf(invoker.needsLoading == 1,3);
		CR2S A 0 A_JumpIf(invoker.firemode == 2,2);
		CR1S A 0;
		"####" E 1 A_StartSound("CrossSelect",1);
		"####" DCBA 1;
		Goto Ready;
	Fire:
		TNT1 A 0
		{
			A_CheckIfAmmo("SoulAmmo",30);
			if (invoker.needsLoading == 1)
			{
				return resolvestate("Reload");
			}
			return resolvestate(null);
		}
		CRB2 A 0 A_JumpIf(invoker.firemode == 2,2);
		CRB1 A 0;
		"####" D 1 Bright A_FireCrossbow;
		"####" E 1 Bright;
		TNT1 A 0 A_SetPitch(pitch-3.5);
		TNT1 A 0 A_ZoomFactor(1.00);
		CRSB ABC 1 A_SetPitch(pitch+1.0);
		CRSB D 1 A_SetPitch(pitch+0.5);
		CRSB E 2 A_WeaponReady(WRF_NOFIRE| WRF_NOBOB); //Allows quick switch
		TNT1 A 0 A_JumpIfInventory("PowerSpeed2",1,1);
		Goto Reload;
		TNT1 A 0 {invoker.needsLoading = 0;}
		Goto Ready;
	Reload:
		TNT1 A 0 A_JumpIf(invoker.needsLoading == 0,"Inspect");
		TNT1 A 0 A_CheckIfAmmo("SoulAmmo",30);
		CRSB FGH 1 A_WeaponReady(WRF_NOFIRE| WRF_NOBOB); //Allows quick switch
		CRSB IJKKLMNOPPQQQRS 1;
		CRSB T 1 A_StartSound("CrossReload",2);
		CRSB UUUV 1;
		TNT1 A 0
		{
			A_StartSound("CrossLit",3);
			invoker.needsLoading = 0;
		}
		CR2R A 0 A_JumpIf(invoker.firemode == 2,2);
		CR1R A 0;
		"####" ABCCCDEFGHIJK 1;
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB); //Allows quick switch
		TNT1 A 0 A_ReFire;
		Goto Ready;
	Inspect:
		CRSB FGH 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB); //Allows quick switch
		CRSB IJ 1 A_WeaponReady;
		CRSB K 15 A_WeaponReady;
		CRSB LMNOPPQQQRS 1 A_WeaponReady;
		CRSB T 1 A_StartSound("CrossReload",2);
		CRSB UUUV 1 A_WeaponReady;
		TNT1 A 0 A_StartSound("CrossLit",3);
		CR2R A 0 A_JumpIf(invoker.firemode == 2,2);
		CR1R A 0;
		"####" ABCCCDEFGHIJK 1 A_WeaponReady;
		Goto Ready;
	NoAmmo:
		TNT1 A 0 A_StartSound("weapons/empty", 3);
		TNT1 A 0 A_JumpIfInventory("SoulAmmo",30,"Reload");
	NoAmmo2:
		CRSB A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_JumpIf(player.cmd.buttons & BT_RELOAD || player.cmd.buttons & BT_ATTACK, "NoAmmo2");
		Goto Ready;
	DualWield:
		TNT1 A 0 A_TakeInventory("StartDualWield",1);
		TNT1 A 0 A_CheckIfAmmo("SoulAmmo",30);
		TNT1 A 0 
		{
			if (invoker.firemode == 2)
			{
				return resolvestate("DualWield2");
			}
			invoker.firemode = 2;
			A_Print("\ciSwitching to explosive arrows.",2);
			return resolvestate(null);
		}
		Goto SwitchingModesAnimation;
	DualWield2:
		TNT1 A 0 {invoker.firemode = 1;}
		TNT1 A 0 A_Print("Switching to penetrating arrows.",2);
		Goto SwitchingModesAnimation;
	SwitchingModesAnimation:
		CRSB FGH 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB); //Allows quick switch
		CRSB IJKLMNOPQRS 1;
		TNT1 A 0 A_StartSound("CrossReload",2);
		CRSB TUUV 1;
		TNT1 A 0 A_StartSound("CrossLit",3);
		CR2R A 0 A_JumpIf(invoker.firemode == 2,2);
		CR1R A 0;
		"####" ABCCDEFGHIJK 1;
		"####" A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB); //Allows quick switch
		"####" A 0 {invoker.needsLoading = 0;}
		Goto Ready;
	Grenadethrowflash:
		CRSS A 0 A_JumpIf(invoker.needsLoading == 1,3);
		CR2S A 0 A_JumpIf(invoker.firemode == 2,2);
		CR1S A 0;
		"####" ABCDE 1;
		"####" Z 22;
		"####" EDCBA 1;
		Stop;
	SprintOverlay:
		CRSK A 0 A_JumpIf(invoker.needsLoading == 1,3);
		CR2R A 0 A_JumpIf(invoker.firemode == 2,2);
		CR1R A 0;
		"####" JIHGF 1;
	KeepSprinting:
		"####" S 1 A_WeaponReadyDX(0,FALSE,FALSE);
		"####" A 0 A_KeepSprinting;
		Goto ReturnFromSprint;
	ReturnFromSprint:
		CRSK A 0 A_JumpIf(invoker.needsLoading == 1,3);
		CR2R A 0 A_JumpIf(invoker.firemode == 2,2);
		CR1R A 0;
		"####" FGHIJ 1;
		Goto Ready;
	KickingFlash:
		CRSK A 0 A_JumpIf(invoker.needsLoading == 1,3);
		CR2R A 0 A_JumpIf(invoker.firemode == 2,2);
		CR1R A 0;
		"####" JIHGFSSSSSSFGHIJ 1; //16
		Goto Ready;
	AirKickingFlash:
		CRSK A 0 A_JumpIf(invoker.needsLoading == 1,3);
		CR2R A 0 A_JumpIf(invoker.firemode == 2,2);
		CR1R A 0;
		"####" JIHGFSSSSSSSSFGHIJ 1; //18
		Goto Ready;
	SlideKickingStart:
		CRSK A 0 A_JumpIf(invoker.needsLoading == 1,3);
		CR2R A 0 A_JumpIf(invoker.firemode == 2,2);
		CR1R A 0;
		"####" JIHGF 1;
	SlideKickingLoop:
		"####" S 1
		{
			if (CountInv("Kicking") == 0)
			{
				return resolvestate("SlideKickingEnd");
			}
			return resolvestate(null);
		}
		Loop;
	SlideKickingEnd:
		CRSK A 0 A_JumpIf(invoker.needsLoading == 1,3);
		CR2R A 0 A_JumpIf(invoker.firemode == 2,2);
		CR1R A 0;
		"####" SSSSSFGHIJ 1; //10
		Goto Ready;
	Spawn:
		CROS A -1;
		Stop;
	}
	
	action void A_FireCrossbow()
	{
		A_ZoomFactor(0.98);
		A_AlertMonstersDX(270);
		A_StartSound("CrossFire",1);
		A_TakeAmmo("SoulAmmo",30);
		invoker.needsLoading = 1;
		A_FireProjectile("ShotgunParticles",random(-12,12),0,-1,0,0,random(-9,9));
		A_FireProjectile("ShotgunParticles",random(-12,12),0,-1,0,0,random(-9,9));
		A_FireProjectile("ShotgunParticles",random(-12,12),0,-1,0,0,random(-9,9));
		A_FireProjectile("ShotgunParticles2",random(-12,12),0,-1,0,0,random(-9,9));
		A_FireProjectile("ShotgunParticles2",random(-12,12),0,-1,0,0,random(-9,9));
		if (invoker.firemode == 1)
		{
			A_FireProjectile("CrossbowProjectile",0,0,0,-3,0,0);
			A_FireProjectile("CrossbowProjectileSmol",10,0,0,-3,0,0);
			A_FireProjectile("CrossbowProjectileSmol",-10,0,0,-3,0,0);
		}
		if (invoker.firemode == 2)
		{
			A_FireProjectile("CrossbowExplosiveProjectile",0,0,0,-3,0,0);
		}
	}
}

class CrossbowProjectile : FastProjectile
{
	Default
	{
		Radius 4;
		Height 2;
		Speed 180;
		DamageFunction 120;
		DamageType "Railgun";
		Scale 1.0;
		Decal "BulletDecalNew1";
		Projectile;
		+RIPPER
		+EXTREMEDEATH
		+BLOODSPLATTER
		+THRUSPECIES
		+MTHRUSPECIES
		+SPECTRAL
		SeeSound "weapons/RLL";
		DeathSound "ARROWHIT";
		Obituary "$OB_MPROCKET";
		Species "Marines";
	}
	States
	{
	Spawn:
		CRBA AAA 1 Bright NoDelay
		{
			A_SpawnItemEx("RedFlareSmall",0,0,3);
			A_SpawnItemEx("CrossbowFlare",-10);
		}
		TNT1 A 0 A_SpawnProjectile("CrossbowFlameTrails", 0, 0, random (0, 40), 2, random (0, -160));
		Loop;
	Crash:
	Death:
		CRBA A 45 Bright A_SpawnItemEx("Ricochet",0,0,-5,0,0,0,0,SXF_NOCHECKPOSITION,0);
	XDeath:
		TNT1 A 0
		{
			A_Explode(20,20);
			A_PlaySound("imp/shotx");
			A_SpawnItemEx("ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
			A_SpawnItemEx("ExplosionParticleSpawner");
			A_SpawnItemEx("RocketFlare",-20);
			A_SpawnItemEx("SmallUnderwaterExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
			A_CustomMissile("CrossbowExplosionFlames", 1, 0, random (0, 360), 2, random (0, 70));
			A_SpawnItemEx("DetectFloorCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
			A_SpawnItemEx("DetectCeilCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
			A_CustomMissile("BDExplosionparticlesSmall", 8, 0, random (0, 180), 2, random (40, 90));
			A_CustomMissile("PlasmaSmoke", 1, 0, random (0, 360), 2, random (0, 160));
		}
		Stop;
	}
}

class CrossbowProjectileSmol: CrossbowProjectile
{
	Default
	{
		Speed 90;
		DamageFunction 44;
		Scale 0.8;
	}
}

class CrossbowExplosiveProjectile: CrossbowProjectile
{
	Default
	{
		Speed 100;
		DamageFunction 300;
		DamageType "Explosive";
		-RIPPER
	}
	States
	{
	Spawn:
		CRBA BBB 1 Bright NoDelay
		{
			A_SpawnItemEx("RedFlareSmall", 0, 0, 3);
			A_SpawnItemEx("CrossbowFlare", -10);
		}
		TNT1 A 0 A_SpawnProjectile("CrossbowFlameTrails", 0, 0, random (0, 40), 2, random (0, -160));
		Loop;
	Crash:
	Death:
	XDeath:
		TNT1 A 0
		{
			A_SpawnItemEx("BarrelExplosionDamage",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
			A_SpawnItemEx("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
			A_SpawnItemEx("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
			A_SpawnItemEx("ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
			A_SpawnItemEx("BarrelKaboom",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
			A_SpawnItemEx("WhiteShockwaveBig");
			Radius_Quake(2, 24, 0, 15, 0);
			A_PlaySound("FAREXPL", 3);
		}
		TNT1 AAAA 0 A_SpawnProjectile("FireworkSFXType2", 64, 0, random (0, 360), 2, random (-30, -60));
		TNT1 AAA 0
		{
			A_SpawnProjectile("HighExplosiveFlames", random (10, 20), 0, random (0, 360), 2, random (-10, -90));
			A_SpawnProjectile("BDExplosionparticles3", 64, 0, random (0, 360), 2, random (-40, -90));
			A_SpawnProjectile("BDExplosionparticles4", 64, 0, random (0, 360), 2, random (-40, -90));
		}
		TNT1 AA 0 
		{
			A_SpawnProjectile("BigBlackSmokeLarger", 20, 0, random (0, 360), 2, random (-10, -90));
			A_SpawnItemEx("TinyBurningPiece", random (-15, 15), random (-15, 15));
			A_SpawnItemEx("TinyBurningPiece2", random (-35, 35), random (-35, 35));
			A_SpawnItemEx("TinyBurningPiece3", random (-45, 45), random (-45, 35));
		}
		TNT1 A 4;
		TNT1 A 0 A_SpawnItemEx("ExplosionSplashSpawner", 0, 0, 0);
		TNT1 AAA 8 A_SpawnProjectile("ExplosionSmoke", 1, 0, random (0, 360), 2, random (-50, -130));
		Stop;
	}
}